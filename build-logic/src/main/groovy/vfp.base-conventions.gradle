plugins {
    id "java"
    id "idea"
    id "maven-publish"

    id "fabric-loom"
}

base {
    group = project.maven_group
    version = project.maven_version
}

repositories {
    mavenCentral()
    maven {
        name = "ViaVersion"
        url = "https://repo.viaversion.com"
    }
    maven {
        name = "Lenni0451"
        url = "https://maven.lenni0451.net/everything"
    }
    maven {
        name = "OpenCollab Snapshots"
        url = "https://repo.opencollab.dev/maven-snapshots/"
    }
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/releases"
    }
    maven {
        name = "Jitpack"
        url = "https://jitpack.io"

        content {
            includeGroup "com.github.Oryxel"
        }
    }
}

configurations {
    vvDependencies

    compileOnly.extendsFrom vvDependencies
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_version}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    vvDependencies "com.viaversion:viaversion-common:5.3.2-SNAPSHOT"
    vvDependencies "com.viaversion:viabackwards-common:5.3.2-SNAPSHOT"
    vvDependencies "com.viaversion:viaaprilfools-common:4.0.2-SNAPSHOT"
    vvDependencies("com.viaversion:vialoader:4.0.3-SNAPSHOT") {
        exclude group: "com.google.guava", module: "guava"
        exclude group: "org.slf4j", module: "slf4j-api"
    }
    vvDependencies "net.raphimc:ViaLegacy:3.0.10-SNAPSHOT"
    vvDependencies("net.raphimc:ViaBedrock:0.0.16-SNAPSHOT") {
        exclude group: "io.jsonwebtoken"
        exclude group: "com.mojang", module: "brigadier"
    }
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

publishing {
    repositories {
        maven {
            name = "Via"
            url = "https://repo.viaversion.com/"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = project.maven_group
            artifactId = project.name
            version = project.maven_version

            from components.java

            pom {
                name = artifactId
                description = project.description
                url = "https://github.com/ViaVersion/ViaFabricPlus"
                licenses {
                    license {
                        name = "GPL-3.0 License"
                        url = "https://github.com/ViaVersion/ViaFabricPlus/blob/main/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "FlorianMichael"
                        name = "EnZaXD"
                        email = "florian.michael07@gmail.com"
                    }
                    developer {
                        id = "RK_01"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/ViaVersion/ViaFabricPlus.git"
                    developerConnection = "scm:git:ssh://github.com/ViaVersion/ViaFabricPlus.git"
                    url = "github.com/ViaVersion/ViaFabricPlus"
                }
            }
        }
    }
}
